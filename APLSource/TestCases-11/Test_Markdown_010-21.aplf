 R←Test_Markdown_010(stopFlag batchFlag);⎕TRAP;parms;filename;markdown;html;cssUrl;searchPattern;blockNo;cssFile;ns
⍝ Specify non-default name for the CSS.
⍝ R gets one of: 0=Okay, 1=test case failed, ¯1=test case was not executed due to the "batchFlag"
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 filename←'Tests\Test_01.md'
 markdown←#.APLTreeUtils.ReadUtf8File filename
 parms←##.PresentAPL.CreateParms
 parms.cssURL←#.FilesAndDirs.PWD
 parms.css←'Tests\PresentAPL_.css'
 (html ns)←##.PresentAPL.MakePresentationFromMarkdown markdown parms

 →PassesIf 0∊⍴ns.report
 searchPattern←'<link rel="stylesheet" type="text/css" href=".*" />'
 blockNo←searchPattern ⎕S 2⍠('Greedy' 0)('Mode' 'L')⊣html
 →PassesIf 1=⍴blockNo
 cssFile←{⍵↑⍨¯1+⍵⍳'"'}(⍴'<link rel="stylesheet" type="text/css" href="')↓(1+⊃blockNo)⊃html
 cssFile←#.FilesAndDirs.NormalizePath cssFile
 →PassesIf cssFile≡'expand'#.FilesAndDirs.NormalizePath parms.css

 R←∆OK
